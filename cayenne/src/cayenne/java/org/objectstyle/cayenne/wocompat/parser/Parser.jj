/**
 * Courtesy of Jeff Martin <reportmill@yahoo.com>  and Josh from Report Mill
 * http://www.reportmill.com/
 */


options {
	STATIC = false;
}

PARSER_BEGIN(Parser)
/* ====================================================================
 * 
 * The ObjectStyle Group Software License, Version 1.0 
 *
 * Copyright (c) 2002-2004 The ObjectStyle Group 
 * and individual authors of the software.  All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer. 
 *
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in
 *    the documentation and/or other materials provided with the
 *    distribution.
 *
 * 3. The end-user documentation included with the redistribution, if
 *    any, must include the following acknowlegement:  
 *       "This product includes software developed by the 
 *        ObjectStyle Group (http://objectstyle.org/)."
 *    Alternately, this acknowlegement may appear in the software itself,
 *    if and wherever such third-party acknowlegements normally appear.
 *
 * 4. The names "ObjectStyle Group" and "Cayenne" 
 *    must not be used to endorse or promote products derived
 *    from this software without prior written permission. For written 
 *    permission, please contact andrus@objectstyle.org.
 *
 * 5. Products derived from this software may not be called "ObjectStyle"
 *    nor may "ObjectStyle" appear in their names without prior written
 *    permission of the ObjectStyle Group.
 *
 * THIS SOFTWARE IS PROVIDED ``AS IS'' AND ANY EXPRESSED OR IMPLIED
 * WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED.  IN NO EVENT SHALL THE OBJECTSTYLE GROUP OR
 * ITS CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF
 * USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT
 * OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 * ====================================================================
 *
 * This software consists of voluntary contributions made by many
 * individuals on behalf of the ObjectStyle Group.  For more
 * information on the ObjectStyle Group, please see
 * <http://objectstyle.org/>.
 *
 */
package org.objectstyle.cayenne.wocompat.parser;

import org.apache.log4j.Logger;
import java.io.*; 
import java.util.*;

/**
 * NeXT/Apple Property List format parser. 
 *
 * <p>Contributed by Jeff Martin <reportmill@yahoo.com> and Josh from Report Mill -
 * <a href="http://www.reportmill.com/">http://www.reportmill.com/</a></p>
 */
public class Parser {
   
    private static Logger logObj = Logger.getLogger(Parser.class);
    
    public Parser() {
        this(System.in);
    }

    public Parser(String filename) throws FileNotFoundException {
        this(new FileInputStream(filename));
    }

    public Parser(File f) throws FileNotFoundException {
        this(new FileInputStream(f));
    }

    public Object propertyList() {
        try {
            return object();
        }
        catch (ParseException pe) {
           logObj.info("Error parsing plist", pe);
           return null;
        }
    }

public static void main(String args[]) {
  Parser r = new Parser();
  Object o = r.propertyList();
  System.out.println(o);
  }

}
PARSER_END(Parser)


SKIP :
{
 	" "
|	"\r"
|	"\n"
|	"\t"
}


TOKEN :
{ 
	< INT:	("-")?(["0"-"9"])+ >
|	< FLOAT: ("-")?((["0"-"9"])*"."(["0"-"9"])+)(["e","E"](["-","+"])?(["0"-"9"])+)? >
}

TOKEN :
{ 
	"="
|	"("
|	")"
|	"{"
|	"}"
|	","
|   ";"
}

SKIP :
{
  "//" : WithinComment
}

<WithinComment> SKIP :
{
  <["\n", "\r"]> : DEFAULT
}

<WithinComment> MORE :
{
  <~["\n" ,"\r"]>
}


TOKEN :
{
	< STRING:  (~[" ","\t","\r","\n","=","(",")","{","}",",",";","\""])+ >
}

SKIP :
{
  "\"" {}: WithinQuotedString
}

<WithinQuotedString> MORE :
{
 < (~["\\","\""])+ > {}
}

<WithinQuotedString> MORE :
{
  "\\\\" { image.setLength(image.length()-1);  }
| "\\a" { int len = image.length(); image.setCharAt(len-2,(char)7); image.setLength(len-1); }
| "\\b" { len = image.length(); image.setCharAt(len-2,'\b'); image.setLength(len-1); }
| "\\t" { len = image.length(); image.setCharAt(len-2,'\t'); image.setLength(len-1); }
| "\\n" { len = image.length(); image.setCharAt(len-2,'\n'); image.setLength(len-1); }
| "\\v" { len = image.length(); image.setCharAt(len-2, (char)11); image.setLength(len-1); }
| "\\f" { len = image.length(); image.setCharAt(len-2,'\f'); image.setLength(len-1); }
| "\\\"" { len = image.length(); image.setCharAt(len-2,'"'); image.setLength(len-1); }
| <"\\"["0"-"7"]["0"-"7"]["0"-"7"]> {
        int oct;
        len = image.length();
        oct = ((int)(image.charAt(len-1)-'0')) +
              8 * ((int)(image.charAt(len-2)-'0')) +
              64 * ((int)(image.charAt(len-3)-'0'));
        image.setCharAt(len-4, (char)oct);
        image.setLength(len-3);
        }
| <"\\U"["0"-"9","a"-"f","A"-"F"]["0"-"9","a"-"f","A"-"F"]
        ["0"-"9","a"-"f","A"-"F"]["0"-"9","a"-"f","A"-"F"] > {
        int dig,c, base=1,hex=0;
        len = image.length();
        for(int i=0; i<4; ++i) {
          c=(int)image.charAt(len-(i+1));
          if (c<='9') c-='0';
            else if (c<='F') c = (c -'A') + 10;
              else c =(c -'a') + 10;
          hex += c*base;
          base*=16;
          }
        image.setCharAt(len-6, (char)hex);
        image.setLength(len-5);
        }

}

<WithinQuotedString> TOKEN :
{
  < QUOTED_STRING: "\"" > { image.setLength(image.length()-1); matchedToken.image = image.toString(); } : DEFAULT
}

Object object() :
{
	Object node;
}
{
		node=array() { return node; }
	|   
		node=dictionary() { return node; }
	|   
		node=leaf() { return node; }
}

Object leaf() :
{}
{ 
  <INT> { return Integer.valueOf(token.image); }
| <FLOAT> { return Double.valueOf(token.image); }
| <QUOTED_STRING> { return token.image; }
| <STRING> { return token.image; }
}

HashMap dictionary() : 
{ 
	HashMap h;
}
{
	"{"  {h = new HashMap(); }
	 (
		keyValuePair(h)
		(
			LOOKAHEAD(2) ";" keyValuePair(h)
    	)* 
    	
    	// last semicolon is optional
    	( ";" )?
    )?
    
	"}" { return h; }
}

void keyValuePair(Map map) :
{
	Object key, val;
}
{
		key = object() 
		"=" 
		val = object() { map.put(key, val); }
}

ArrayList  array() : 
{ ArrayList v;
  Object o;
}
{
   "(" { v = new ArrayList(); } (
      o=object() { v.add(o); }
      ("," o=object() { v.add(o); } )*
     )?
   ")" {return v;}
}